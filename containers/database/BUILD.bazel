load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")
load("@aspect_bazel_lib//lib:tar.bzl", "mtree_mutate", "tar")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load", "oci_push")
load("//:bazel/common.bzl", "BUILD_DIRECTORY", "IMAGE_LABELS", "REPO_NAME")
load("//:bazel/docker.bzl", "docker_run_and_extract")
load("//:bazel/registries.bzl", "REPOSITORIES")
load("//:third_party/vmangos.bzl", "SOURCE_REF_LABELS")

docker_run_and_extract(
    name = "build",
    src = "//containers/builder:docker_run_image",
    command = [
        "bash",
        "/opt/run/database.sh",
    ],
    out_dir = "{d}/db".format(d = BUILD_DIRECTORY),
)

mtree_mutate(
    name = "build_outputs",
    mtree = ":build.mtree",
    ownername = "mysql",
)

tar(
    name = "build_layer",
    srcs = [":build"],
    mtree = "build_outputs.mtree",
)

oci_image(
    name = "image",
    base = "@mariadb_linux_amd64",
    env = {
        "REALMLIST_NAME": "VMaNGOS",
        "REALMLIST_ADDRESS": "127.0.0.1",
        "REALMLIST_PORT": "8085",
        "REALMLIST_ICON": "1",
        "REALMLIST_TIMEZONE": "0",
        "REALMLIST_ALLOWED_SECURITY_LEVEL": "0",
    },
    labels = {
        "org.opencontainers.image.title": "VMaNGOS",
        "org.opencontainers.image.description": "A VMaNGOS DB container image ready-to-use",
    } | IMAGE_LABELS | SOURCE_REF_LABELS,
    tars = [
        ":build_layer",
    ],
)

oci_load(
    name = "load",
    image = ":image",
    repo_tags = ["{n}-database:wip".format(n = REPO_NAME)],
)

expand_template(
    name = "stamped",
    out = "_stamped.tags.txt",
    stamp_substitutions = {"0.0.0": "{{STABLE_CALVER}}"},
    template = ["0.0.0"],
)

[
    oci_push(
        name = "push_db_{a}".format(a = alias),
        image = ":image",
        remote_tags = ":stamped",
        repository = "{r}/{n}db".format(
            n = REPO_NAME,
            r = repository,
        ),
    )
    for alias, repository in REPOSITORIES.items()
]
